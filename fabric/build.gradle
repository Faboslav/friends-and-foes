plugins {
	id "com.github.johnrengelman.shadow" version "8.1.1"
}

allprojects {
	archivesBaseName = "${mod_id}-fabric-mc${minecraft_version}"
}

architectury {
	platformSetupLoomIde()
	fabric()
}

loom {
	accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
	common
	shadowCommon
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentFabric.extendsFrom common
}

dependencies {
	// Core dependencies
	modImplementation("net.fabricmc:fabric-loader:${fabric_loader_version}")
	modApi("net.fabricmc.fabric-api:fabric-api:${fabric_api_version}+${mecraft_version}")

	// Mixin extras (https://github.com/LlamaLad7/MixinExtras)
	include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${mixin_extras_version}")))

	// Reach entity attributes (https://github.com/JamiesWhiteShirt/reach-entity-attributes)
	include(modImplementation("com.jamieswhiteshirt:reach-entity-attributes:${project.reach_entity_attributes_version}"))

	// Mod menu (https://modrinth.com/mod/modmenu)
	modImplementation("com.terraformersmc:modmenu:${mod_menu_version}") { transitive = false }

	// Cloth config (https://www.curseforge.com/minecraft/mc-mods/cloth-config)
	modImplementation("me.shedaniel.cloth:cloth-config:${cloth_config_version}")
	modImplementation("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") { transitive = false }

	// Trinkets (https://www.curseforge.com/minecraft/mc-mods/trinkets)
	modImplementation("dev.emi:trinkets:${trinkets_version}") { transitive = false }

	// Fix for CCA (https://github.com/Ladysnake/Cardinal-Components-API)
	modApi("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cca_version}") { transitive = false }
	modApi("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cca_version}") { transitive = false }

	// Auth (https://github.com/DJtheRedstoner/DevAuth)
	modRuntimeOnly("me.djtheredstoner:DevAuth-fabric:${devauth_version}")

	common(project(path: ":common", configuration: "namedElements")) { transitive false }
	shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

shadowJar {
	exclude "architectury.common.json"

	configurations = [project.configurations.shadowCommon]
	archiveClassifier.set("dev-shadow")
}

remapJar {
	injectAccessWidener = true
	inputFile.set shadowJar.archiveFile
	dependsOn shadowJar
	archiveClassifier.set(null)
}

jar {
	archiveClassifier.set("dev")
}

sourcesJar {
	def commonSources = project(":common").sourcesJar
	dependsOn commonSources
	from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

repositories {
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}

	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"

		content {
			includeGroup "maven.modrinth"
		}
	}

	maven {
		name = "TerraformersMC"
		url "https://maven.terraformersmc.com/releases/"
	}

	maven {
		name = "Ladysnake Libs"
		url = 'https://maven.ladysnake.org/releases'
	}

	maven { url "https://www.cursemaven.com" }

	maven { url = "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1" }

	maven {
		url "https://maven.jamieswhiteshirt.com/libs-release"
		content {
			includeGroup "com.jamieswhiteshirt"
		}
	}
}
