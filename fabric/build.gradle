plugins {
	id "com.github.johnrengelman.shadow" version "8.1.1"
}

allprojects {
	archivesBaseName = "${rootProject.mod_id}-fabric-mc${rootProject.minecraft_version}"
}

configurations.all {
	resolutionStrategy {
		force "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
	}
}

architectury {
	platformSetupLoomIde()
	fabric()
}

loom {
	accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
	common
	shadowCommon
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentFabric.extendsFrom common
}

dependencies {
	// Core dependencies
	modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
	modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
	
	// Trinkets
	modApi("dev.emi:trinkets:${rootProject.trinkets_version}")

	include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${rootProject.mixin_extras_version}")))

	// Config dependencies
	modApi("com.terraformersmc:modmenu:${project.mod_menu_version}")
	modApi("me.shedaniel.cloth:cloth-config:${rootProject.cloth_config_version}")
	modApi("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_config_version}")

	// For faster development https://www.cursemaven.com/
	// https://www.curseforge.com/minecraft/mc-mods/lazydfu/files
	modRuntimeOnly "curse.maven:lazydfu-433518:3821870"
	//modRuntimeOnly "curse.maven:disablecustomworldsadvice-401978:4444372"
	// modRuntimeOnly("me.djtheredstoner:DevAuth-fabric:1.1.2")

	common(project(path: ":common", configuration: "namedElements")) { transitive false }
	shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
	inputs.property 'modId', rootProject.mod_id
	inputs.property 'modName', rootProject.mod_name
	inputs.property 'modDescription', rootProject.mod_description
	inputs.property 'modVersion', rootProject.mod_version
	inputs.property 'modAuthor', rootProject.mod_author

	filesMatching("fabric.mod.json") {
		expand([
			'modId'         : rootProject.mod_id,
			'modName'       : rootProject.mod_name,
			'modDescription': rootProject.mod_description,
			'modVersion'    : rootProject.mod_version,
			'modAuthor'     : rootProject.mod_author
		])
	}
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
	injectAccessWidener = true
	inputFile.set shadowJar.archiveFile
	dependsOn shadowJar
	archiveClassifier.set(null)
}

jar {
	archiveClassifier.set("dev")
}

sourcesJar {
	def commonSources = project(":common").sourcesJar
	dependsOn commonSources
	from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

repositories {
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}

	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"

		content {
			includeGroup "maven.modrinth"
		}
	}

	maven {
		name = "TerraformersMC"
		url "https://maven.terraformersmc.com/releases/"
	}

	maven {
		name = "Ladysnake Libs"
		url = 'https://maven.ladysnake.org/releases'
	}

	maven { url "https://www.cursemaven.com" }

	maven { url = "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1" }
}

publishing {
	publications {
		mavenFabric(MavenPublication) {
			artifactId = rootProject.mod_id + "-" + project.name
			from components.java
		}
	}

	repositories {
	}
}
