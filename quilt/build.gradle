plugins {
	id "com.github.johnrengelman.shadow" version "7.1.2"
}

allprojects {
	archivesBaseName = "${rootProject.mod_id}-quilt-mc${rootProject.minecraft_version}"
}

repositories {
	maven { url "https://maven.quiltmc.org/repository/release/" }
}

architectury {
	platformSetupLoomIde()
	loader("quilt")
}

loom {
	accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
	common
	shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentQuilt.extendsFrom common
}

dependencies {
	modImplementation "org.quiltmc:quilt-loader:${rootProject.quilt_loader_version}"
	modImplementation "com.github.Chocohead:Fabric-ASM:${rootProject.fabric_asm_version}"
	modApi "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${rootProject.quilt_fabric_api_version}"
	modApi("dev.architectury:architectury-fabric:${rootProject.architectury_version}") {
		exclude group: "net.fabricmc"
		exclude group: "net.fabricmc.fabric-api"
	}

	common(project(path: ":common", configuration: "namedElements")) { transitive false }
	shadowCommon(project(path: ":common", configuration: "transformProductionQuilt")) { transitive false }

	// For faster development https://www.cursemaven.com/
	modRuntimeOnly "maven.modrinth:lazydfu:0.1.2"
	modRuntimeOnly "maven.modrinth:starlight:1.0.2+1.18.2"
	modRuntimeOnly "curse.maven:disablecustomworldsadvice-401978:3677546"
}

processResources {
	inputs.property "group", rootProject.maven_group
	inputs.property 'modId', rootProject.mod_id
	inputs.property 'modName', rootProject.mod_name
	inputs.property 'modDescription', rootProject.mod_description
	inputs.property 'modVersion', rootProject.mod_version
	inputs.property 'modAuthor', rootProject.mod_author

	filesMatching("quilt.mod.json") {
		expand([
			'group'        : rootProject.maven_group,
			'modId'         : rootProject.mod_id,
			'modName'       : rootProject.mod_name,
			'modDescription': rootProject.mod_description,
			'modVersion'    : rootProject.mod_version,
			'modAuthor'     : rootProject.mod_author
		])
	}
}

shadowJar {
	exclude "architectury.common.json"

	configurations = [project.configurations.shadowCommon]
	classifier "dev-shadow"
}

remapJar {
	injectAccessWidener = true
	input.set shadowJar.archiveFile
	dependsOn shadowJar
	classifier null
}

jar {
	classifier "dev"
}

sourcesJar {
	def commonSources = project(":common").sourcesJar
	dependsOn commonSources
	from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

repositories {
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}

	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"

		content {
			includeGroup "maven.modrinth"
		}
	}

	maven { url "https://www.cursemaven.com" }
}

publishing {
	publications {
		mavenFabric(MavenPublication) {
			artifactId = rootProject.mod_id + "-" + project.name
			from components.java
		}
	}

	repositories {
	}
}
