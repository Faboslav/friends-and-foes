plugins {
	id 'java'
	id 'idea'
	id 'dev.architectury.loom'
	id 'architectury-plugin'
}

def minecraftVersion = stonecutter.current.version

def modrinth(String name, String version) {
	return "maven.modrinth:$name:$version"
}

def prop(String name) {
	return rootProject.project(stonecutter.current.project).property(name).toString()
}

base {
	archivesName.set("${mod_id}-common-mc${minecraftVersion}-${mod_version}")
}

architectury {
	common prop('enabled_platforms').split(',')
}

loom {
	silentMojangMappingsLicense()

	accessWidenerPath = getRootProject().file("common/src/main/resources/${mod_id}.accesswidener")

	runConfigs.all {
		ideConfigGenerated true
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraftVersion}"
	mappings loom.layered() {
		officialMojangMappings()

		if (prop('parchment_version') != '') {
			parchment("org.parchmentmc.data:parchment-${prop('minecraft_version')}:${prop('parchment_version')}@zip")
		}
	}

	// General
	modImplementation("net.fabricmc:fabric-loader:${prop('fabric_loader_version')}")

	// Required client
	modImplementation(modrinth("yacl", "${prop('yacl_version')}-fabric"))
}

processResources {
	def expandProps = [
		"modJavaVersion"  : prop('mod_java_version'),
		'modId'           : mod_id,
		'modVersion'      : mod_version,
		"minecraftVersion": prop('minecraft_version'),
	]

	filesMatching(['*.mixins.json']) {
		expand expandProps
	}

	inputs.properties(expandProps)
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.toVersion(prop('mod_java_version'))
	targetCompatibility = JavaVersion.toVersion(prop('mod_java_version'))
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.getRelease().set(prop('mod_java_version') as Integer)
}

tasks.withType(Jar).configureEach {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

repositories {
	// CurseForge
	maven {
		name 'CurseForge'
		url "https://www.cursemaven.com"
	}

	// Modrinth
	maven {
		url "https://api.modrinth.com/maven"
		content {
			includeGroup("maven.modrinth")
		}
	}

	// Mixin Extras
	maven {
		url 'https://jitpack.io'
		content {
			includeGroup 'com.github.llamalad7'
		}
	}

	// YACL
	maven { url "https://maven.isxander.dev/releases" }
	maven { url "https://maven.isxander.dev/snapshots" }
	maven { url 'https://thedarkcolour.github.io/KotlinForForge/' }
	maven { url = "https://maven.quiltmc.org/repository/release" }

	// Parchment
	maven {
		name = 'ParchmentMC'
		url = 'https://maven.parchmentmc.org'
	}
}